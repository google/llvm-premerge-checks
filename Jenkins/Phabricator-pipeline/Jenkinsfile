// Copyright 2019 Google LLC
//
// Licensed under the the Apache License v2.0 with LLVM Exceptions (the "License");
// you may not use this file except in compliance with the License.
// You may obtain a copy of the License at
//
//     https://llvm.org/LICENSE.txt
//
// Unless required by applicable law or agreed to in writing, software
// distributed under the License is distributed on an "AS IS" BASIS,
// WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
// See the License for the specific language governing permissions and
// limitations under the License.

def success = true

pipeline {
    agent { label 'linux' }
    parameters {
        string(name: 'DIFF_ID')
        string(name: 'PHID')
    }
    environment {
            CONDUIT_TOKEN = credentials('phabricator-conduit-token')
            PHABRICATOR_HOST = 'https://reviews.llvm.org'
            PHAB_LOG = "${WORKSPACE}/build/.phabricator-comment"
            SCRIPT_DIR = "${WORKSPACE}/llvm-premerge-checks/scripts"
            BUILD_ID = "${JOB_BASE_NAME}-${BUILD_NUMBER}"
            TARGET_DIR = "/mnt/nfs/results/${BUILD_ID}"
            RESULT_URL = "http://results.llvm-merge-guard.org/${BUILD_ID}"
            DIFF_JSON = "${WORKSPACE}/build/diff.json"
    }
    stages {
        stage("build info"){
            steps {
                echo "Building diff ${DIFF_ID} with PHID ${PHID}"
                script {
                    def revision_phid = sh(script:
                        'echo \'{"constraints":{"ids":[' + DIFF_ID + ']}}\' | '+
                        'arc call-conduit --conduit-uri=$PHABRICATOR_HOST --conduit-token $CONDUIT_TOKEN differential.diff.search | ' +
                        'jq -r ".response.data[0].fields.revisionPHID"', returnStdout: true).trim()
                    def drev_id = sh(script:
                        'echo \'{"constraints":{"phids":["' + revision_phid + '"]}}\' | ' +
                        'arc call-conduit --conduit-uri=$PHABRICATOR_HOST --conduit-token $CONDUIT_TOKEN differential.revision.search | ' +
                        'jq -r ".response.data[0].id"', returnStdout: true).trim()
                    currentBuild.displayName += " D${drev_id}"
                    currentBuild.description = "<a href='https://reviews.llvm.org/D${drev_id}'>D${drev_id}</a>"
                }
                // Report versions of the installed packages.
                sh 'dpkg -l'
                sh 'clang --version'
                sh 'python --version'
            }
        }
        stage("git checkout"){
            steps {
                git url: 'https://github.com/llvm/llvm-project.git'
                sh 'git clean -fdx'
                sh 'mkdir -p llvm-premerge-checks'
                dir("llvm-premerge-checks")
                {
                    git url: 'https://github.com/google/llvm-premerge-checks.git'
                }
                sh 'rm -rf build || true'
                sh 'mkdir -p build'
                sh 'mkdir -p "${TARGET_DIR}"'
            }
        }
        stage('arc patch'){
            steps {
                sh "python3 ${SCRIPT_DIR}/phabtalk/apply_patch.py --comment-file ${PHAB_LOG}"
            }
        }
        stage('CMake') {
            steps {
                sh "${SCRIPT_DIR}/run_cmake.sh"
            }
        }
        stage('ninja all') {
            steps {
                script {
                    try {
                        sh(script: "${SCRIPT_DIR}/run_ninja.sh all")
                    } catch (e) {
                          success = false;
                          echo e.toString()
                    }
                }
            }
        }
        stage('ninja check-all') {
            steps {
                script {
                    try {
                        sh(script: "${SCRIPT_DIR}/run_ninja.sh check-all")
                    } catch (e) {
                        success = false;
                        echo e.toString()
                    }
                }
            }
        }
        stage('linters') {
            steps {
                sh "${SCRIPT_DIR}/lint.sh"
            }
        }
    }
    post { 
        always {
            script {
                if (success) {
                    currentBuild.result = "SUCCESS"
                } else {
                    currentBuild.result = "FAILURE"
                }
            }
            echo "Console log is available at ${RESULT_URL}"
            dir("${TARGET_DIR}") {
                // copy console log to result folder
                sh "wget -qO console-log.txt http://jenkins-ui.jenkins.svc.cluster.local:8080/job/${JOB_BASE_NAME}/${BUILD_NUMBER}/consoleText"
                // keep a copy of the answer from Phabricator for debugging
                // TODO: move all file copy operations here
                sh """
                    set -eu
                    if [ -f "${DIFF_JSON}" ]; then
                        cp  ${DIFF_JSON} .
                    fi
                """
            }
            /// send results to Phabricator
            sh """${SCRIPT_DIR}/phabtalk/phabtalk.py "${PHID}" "${DIFF_ID}" \
                --workspace "${WORKSPACE}" \
                --conduit-token "${CONDUIT_TOKEN}" \
                --test-result-file "test-results.xml" \
                --host "${PHABRICATOR_HOST}/api/" \
                --buildresult ${currentBuild.result} \
                --clang-format-patch "clang-format.patch" \
                --clang-tidy-result "clang-tidy.txt" \
                --results-dir "${TARGET_DIR}" \
                --results-url "${RESULT_URL}"
                """
        }
    }    
}
